apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'

//https://gist.github.com/shakalaca/6422811
android {
    applicationVariants.all { variant ->

        variant.assemble.doLast {
            delete fileTree('src/main/java/com/cylonid/nativealpha') {
                include '**/__WebViewActivity_*.java'
            }
            delete fileTree('src/main') {
                include '**/AndroidManifest.xml'
            }
            ant.move(file: 'src/main/AndroidManifest_original.xml', tofile: 'src/main/AndroidManifest.xml')

        }
    }

    compileSdkVersion 32
    buildToolsVersion "32.1.0-rc1"
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }
    flavorDimensions "default"
    productFlavors {
        extended {
            dimension "default"
            applicationIdSuffix ".pro"
            resValue "string", "app_name_unicode", "Native α+"
        }
        standard {
            dimension "default"
            isDefault true
            resValue "string", "app_name_unicode", "Native α"
        }


    }

    defaultConfig {
        applicationId "com.cylonid.nativealpha"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 1200
        versionName "1.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildFeatures {
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
        debug {

        }
        applicationVariants.all {
                // this method is use to rename your release apk only
            variant ->
                variant.outputs.each {
                        // on below line we are setting a name to our apk
                    output ->
                        // on below line we are specifying our app name.
                        project.ext { appName = 'NativeAlpha' }
                        // on below line we are adding the formatted date to our apk file name.
                        def formattedDate = new Date().format('yyyy-MM-dd-HH-mm-ss')
                        // on below line we are creating a new name for our apk.
                        def newName = output.outputFile.name
                        // on below line we are replacing our previous name with our app name.
                        newName = newName.replace("app-", "$project.ext.appName-")
                        // on below line we are replacing -release with our formatted date.
                        newName = newName.replace("-release", "-release-" + "v" + variant.versionName)
                        // at last we are setting our apk name to it.
                        output.outputFileName = newName

                }
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}
repositories {
    maven {
        url 'https://gitlab.com/api/v4/projects/8817162/packages/maven'
    }
    mavenCentral()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.mikhaellopez:circularprogressbar:3.1.0'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'org.adblockplus:adblock-android-webview:5.0.0'
    implementation 'com.github.ihimanshurawat:Hasher:1.2'
    implementation "com.google.android.material:material:1.5.0"
    implementation 'androidx.navigation:navigation-fragment:2.4.1'
    implementation 'androidx.navigation:navigation-ui:2.4.1'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'io.github.medyo:android-about-page:2.0.0'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'com.jakewharton:process-phoenix:2.1.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'org.json:json:20220320'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.mikepenz:aboutlibraries:10.1.0'
    implementation 'com.mikepenz:aboutlibraries-core:10.1.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
}

int NUM_OF_CONTAINERS = 8
String placeholder = "<!--Placeholder-->"

0.upto(NUM_OF_CONTAINERS - 1) { int i ->
    def trTask = tasks.register("createWebViewclass${i}", Copy) {
        from 'src/main/java/com/cylonid/nativealpha/WebViewActivity.java'
        into 'src/main/java/com/cylonid/nativealpha'
        filter {
            String line -> line.replaceAll("WebViewActivity", "__WebViewActivity_${i}")
        }
        rename { String fileName ->
            fileName.replace("WebViewActivity.java", "__WebViewActivity_${i}.java")
        }
    }
    preBuild.dependsOn trTask
}

def renameManifest = tasks.register("renameManifest") {
    ant.move(file: 'src/main/AndroidManifest.xml', tofile: 'src/main/AndroidManifest_original.xml')
}
preBuild.dependsOn renameManifest


def generateWebViewActivities = tasks.register("extendAndroidManifest", Copy) {
    from 'src/main/AndroidManifest_original.xml'
    into 'src/main'

    String replacement = ""
    for (int i = 0; i < NUM_OF_CONTAINERS; i++) {
        replacement += "        <activity\n" +
                "            android:name=\".__WebViewActivity_" + String.valueOf(i) + "\"\n" +
                "            android:label=\"@string/app_name_unicode\"\n" +
                "            android:parentActivityName=\".MainActivity\"\n" +
                "            android:icon=\"@mipmap/native_alpha\"\n" +
                "            android:roundIcon=\"@mipmap/native_alpha\"\n" +
                "            android:theme=\"@style/AppTheme.NoActionBar.Webapp\"\n" +
                "            android:configChanges=\"orientation|screenSize\"\n" +
                "            android:launchMode=\"standard\"\n" +
                "            android:process=\":web_sandbox_" + String.valueOf(i) + "\"\n" +
                "            android:documentLaunchMode=\"intoExisting\"\n" +
                "         />\n";
    }
    filter {
        String line -> line.replaceAll(placeholder, replacement)
    }
    rename { String fileName ->
        fileName.replace("_original", "")
    }
}
preBuild.dependsOn generateWebViewActivities


